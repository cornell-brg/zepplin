//========================================================================
// SSRenameTable_test.v
//========================================================================
// A testbench for our multi rename table

`include "hw/decode_issue/SSRenameTable.v"
`include "intf/CommitNotif.v"
`include "intf/CompleteNotif.v"
`include "test/fl/TestCaller.v"
`include "test/fl/TestMPub.v"
`include "test/TestUtils.v"

import TestEnv::*;

//========================================================================
// SSRenameTableTestSuite
//========================================================================
// A test suite for the rename table

module SSRenameTableTestSuite #(
  parameter p_suite_num        = 0,
  parameter p_num_phys_regs    = 36,
  parameter p_num_fe_lanes     = 2,
  parameter p_num_be_lanes     = 2
);

  localparam p_phys_addr_bits = $clog2( p_num_phys_regs );

  string suite_name = $sformatf("%0d: SSRenameTableTestSuite_%0d", 
                                p_suite_num, p_num_phys_regs);
  
  //----------------------------------------------------------------------
  // Setup
  //----------------------------------------------------------------------

  logic clk, rst;
  TestUtils t( .* );

  //----------------------------------------------------------------------
  // Instantiate design under test
  //----------------------------------------------------------------------

  logic                  [4:0] dut_alloc_areg  [p_num_fe_lanes];
  logic [p_phys_addr_bits-1:0] dut_alloc_preg  [p_num_fe_lanes];
  logic [p_phys_addr_bits-1:0] dut_alloc_ppreg [p_num_fe_lanes];
  // alloc_try and alloc_val are tied together here (driven by the same
  // TestCaller .en) -- this suite never needs to exercise a lane that
  // wants to allocate (try) without actually firing (val) this cycle.
  logic                        dut_alloc_go    [p_num_fe_lanes];
  logic                        dut_alloc_rdy   [p_num_fe_lanes];

  logic                  [4:0] dut_lookup_new_inst_areg    [p_num_fe_lanes][2];
  logic [p_phys_addr_bits-1:0] dut_lookup_new_inst_preg    [p_num_fe_lanes][2];
  logic                        dut_lookup_new_inst_pending [p_num_fe_lanes][2];

  CompleteNotif #(
    .p_phys_addr_bits (p_phys_addr_bits)
  ) complete_notif [p_num_be_lanes] ();

  CommitNotif #(
    .p_phys_addr_bits (p_phys_addr_bits)
  ) commit_notif [p_num_be_lanes] ();

  SSRenameTable #(
    .p_num_phys_regs (p_num_phys_regs),
    .p_num_fe_lanes  (p_num_fe_lanes),
    .p_num_be_lanes  (p_num_be_lanes)
  ) dut (
    .alloc_areg  (dut_alloc_areg),
    .alloc_preg  (dut_alloc_preg),
    .alloc_ppreg (dut_alloc_ppreg),
    .alloc_try   (dut_alloc_go),
    .alloc_val   (dut_alloc_go),
    .alloc_rdy   (dut_alloc_rdy),

    .lookup_new_inst_areg    (dut_lookup_new_inst_areg),
    .lookup_new_inst_preg    (dut_lookup_new_inst_preg),
    .lookup_new_inst_pending (dut_lookup_new_inst_pending),

    .complete    (complete_notif),
    .commit      (commit_notif),
    .*
  );

  //----------------------------------------------------------------------
  // Allocation
  //----------------------------------------------------------------------

  typedef logic [4:0] t_alloc_call_msg;

  typedef struct packed {
    logic [p_phys_addr_bits-1:0] preg;
    logic [p_phys_addr_bits-1:0] ppreg;
  } t_alloc_ret_msg;

  t_alloc_ret_msg alloc_ret_msg [p_num_fe_lanes];

  genvar i;
  generate
    for( i = 0; i < p_num_fe_lanes; i++ ) begin: ALLOC_PORT
      assign alloc_ret_msg[i].preg  = dut_alloc_preg[i];
      assign alloc_ret_msg[i].ppreg = dut_alloc_ppreg[i];

      TestCaller #(
        .t_call_msg (t_alloc_call_msg),
        .t_ret_msg  (t_alloc_ret_msg)
      ) alloc_caller (
        .call_msg (dut_alloc_areg[i]),
        .ret_msg  (alloc_ret_msg[i]),
        .en       (dut_alloc_go[i]),
        .rdy      (dut_alloc_rdy[i]),
        .*
      );
    end
  endgenerate

  t_alloc_ret_msg msgs_from_alloc      [p_num_fe_lanes];
  logic     [4:0] msgs_from_alloc_areg [p_num_fe_lanes];
  logic           msgs_from_alloc_val  [p_num_fe_lanes];

  generate
    for( i = 0; i < p_num_fe_lanes; i++ ) begin
      always @( posedge clk ) begin
        #1;
        if( msgs_from_alloc_val[i] ) begin
          ALLOC_PORT[i].alloc_caller.call(
            msgs_from_alloc_areg[i],
            msgs_from_alloc[i]
          );
        end

        // verilator lint_off BLKSEQ
        msgs_from_alloc_val[i] = 1'b0;
        // verilator lint_on BLKSEQ
      end

      initial begin
        msgs_from_alloc_val[i] = 1'b0;
      end
    end
  endgenerate

  t_alloc_ret_msg alloc_task_msg [p_num_fe_lanes];

  task alloc(
    input logic                  [4:0] areg  [p_num_fe_lanes],
    input logic [p_phys_addr_bits-1:0] preg  [p_num_fe_lanes],
    input logic [p_phys_addr_bits-1:0] ppreg [p_num_fe_lanes],
    input logic                        valid [p_num_fe_lanes]
  );
    // Don't use `wait fork` here -- Verilator's --timing scheduler hits
    // an internal codegen bug (undeclared __Vtrigprevexpr_* symbol in a
    // split translation unit) on it in this file.
    automatic int pending = p_num_fe_lanes;
    for( int j = 0; j < p_num_fe_lanes; j++ ) begin
      automatic int jj = j;
      fork
        begin
          alloc_task_msg[jj].preg  = preg[jj];
          alloc_task_msg[jj].ppreg = ppreg[jj];
          msgs_from_alloc_areg[jj] = areg[jj];
          msgs_from_alloc[jj]      = alloc_task_msg[jj];
          msgs_from_alloc_val[jj]  = valid[jj];
          while(msgs_from_alloc_val[jj])
            #1;
          pending--;
        end
      join_none
    end
    while( pending > 0 )
      #1;
  endtask

  //----------------------------------------------------------------------
  // Lookup new inst source registers
  //----------------------------------------------------------------------

  // Purely combinational (no en/rdy handshake): the DUT drives
  // lookup_new_inst_preg/pending live from whatever lookup_new_inst_areg
  // currently holds (see test_case_5_pending_lifecycle for the pending
  // side).

  task lookup_new_inst_srcs(
    input logic                  [4:0] areg     [p_num_fe_lanes][2],
    input logic [p_phys_addr_bits-1:0] exp_preg [p_num_fe_lanes][2],
    input logic                        valid    [p_num_fe_lanes][2]
  );
    dut_lookup_new_inst_areg = areg;
    // Sample after the same posedge the alloc() responder's own
    // `always @(posedge clk) #1; ...call(...)` uses to assert
    // alloc_try/alloc_areg, so same-cycle alloc forwarding (see
    // SSRenameTable.v's "Alloc forwarding from earlier lanes" comment)
    // is actually visible when this runs concurrently with alloc().
    @( posedge clk );
    #2;
    for( int j = 0; j < p_num_fe_lanes; j++ ) begin
      for( int k = 0; k < 2; k++ ) begin
        if( valid[j][k] ) begin
          `CHECK_EQ(dut_lookup_new_inst_preg[j][k], exp_preg[j][k]);
        end
      end
    end
  endtask

  task lookup_new_inst_pending(
    input logic [4:0] areg        [p_num_fe_lanes][2],
    input logic       exp_pending [p_num_fe_lanes][2],
    input logic       valid       [p_num_fe_lanes][2]
  );
    dut_lookup_new_inst_areg = areg;
    // Sample after the same posedge the alloc() responder's own
    // `always @(posedge clk) #1; ...call(...)` uses to assert
    // alloc_try/alloc_areg, so same-cycle alloc forwarding (see
    // SSRenameTable.v's "Alloc forwarding from earlier lanes" comment)
    // is actually visible when this runs concurrently with alloc().
    @( posedge clk );
    #2;
    for( int j = 0; j < p_num_fe_lanes; j++ ) begin
      for( int k = 0; k < 2; k++ ) begin
        if( valid[j][k] ) begin
          `CHECK_EQ(dut_lookup_new_inst_pending[j][k], exp_pending[j][k]);
        end
      end
    end
  endtask

  //----------------------------------------------------------------------
  // Complete
  //----------------------------------------------------------------------

  typedef logic [p_phys_addr_bits-1:0] t_complete_msg;

  t_complete_msg complete_msg     [p_num_be_lanes];
  logic          complete_msg_val [p_num_be_lanes];

  generate
    for( i = 0; i < p_num_be_lanes; i++ ) begin
      assign complete_notif[i].preg  = complete_msg[i];
      assign complete_notif[i].waddr = 'x;
      assign complete_notif[i].wdata = 'x;
      assign complete_notif[i].wen   = 'x;
      assign complete_notif[i].val   = complete_msg_val[i];
    end
  endgenerate

  logic  [4:0] unused_complete_waddr [p_num_be_lanes];
  logic [31:0] unused_complete_wdata [p_num_be_lanes];
  logic        unused_complete_wen   [p_num_be_lanes];

  generate
    for( i = 0; i < p_num_be_lanes; i++ ) begin
      assign unused_complete_waddr[i] = complete_notif[i].waddr;
      assign unused_complete_wdata[i] = complete_notif[i].wdata;
      assign unused_complete_wen[i]   = complete_notif[i].wen;
    end
  endgenerate

  TestMPub #(
    .t_msg      (t_complete_msg),
    .p_num_msgs (p_num_be_lanes)
  ) complete_pub (
    .msg (complete_msg),
    .val (complete_msg_val),
    .*
  );

  t_complete_msg msg_to_complete_pub     [p_num_be_lanes];
  logic          msg_to_complete_pub_val [p_num_be_lanes];

  task complete(
    input t_complete_msg preg [p_num_be_lanes],
    input logic          val  [p_num_be_lanes]
  );
    for( int j = 0; j < p_num_be_lanes; j++ ) begin
      msg_to_complete_pub[j]     = preg[j];
      msg_to_complete_pub_val[j] = val[j];
    end

    complete_pub.pub( msg_to_complete_pub, msg_to_complete_pub_val );
  endtask

  //----------------------------------------------------------------------
  // Commit
  //----------------------------------------------------------------------

  typedef logic [p_phys_addr_bits-1:0] t_commit_msg;

  t_commit_msg commit_msg     [p_num_be_lanes];
  logic        commit_msg_val [p_num_be_lanes];

  generate
    for( i = 0; i < p_num_be_lanes; i++ ) begin
      assign commit_notif[i].ppreg = commit_msg[i];
      assign commit_notif[i].pc    = 'x;
      assign commit_notif[i].waddr = 'x;
      assign commit_notif[i].wdata = 'x;
      assign commit_notif[i].wen   = 'x;
      assign commit_notif[i].val   = commit_msg_val[i];
    end
  endgenerate

  logic [31:0] unused_commit_pc    [p_num_be_lanes];
  logic  [4:0] unused_commit_waddr [p_num_be_lanes];
  logic [31:0] unused_commit_wdata [p_num_be_lanes];
  logic        unused_commit_wen   [p_num_be_lanes];

  generate
    for( i = 0; i < p_num_be_lanes; i++ ) begin
      assign unused_commit_pc[i]    = commit_notif[i].pc;
      assign unused_commit_waddr[i] = commit_notif[i].waddr;
      assign unused_commit_wdata[i] = commit_notif[i].wdata;
      assign unused_commit_wen[i]   = commit_notif[i].wen;
    end
  endgenerate

  TestMPub #(
    .t_msg      (t_commit_msg),
    .p_num_msgs (p_num_be_lanes)
  ) commit_pub (
    .msg (commit_msg),
    .val (commit_msg_val),
    .*
  );

  t_commit_msg msg_to_commit_pub     [p_num_be_lanes];
  logic        msg_to_commit_pub_val [p_num_be_lanes];

  task commit(
    input t_commit_msg ppreg [p_num_be_lanes],
    input logic        val   [p_num_be_lanes]
  );
    for( int j = 0; j < p_num_be_lanes; j++ ) begin
      msg_to_commit_pub[j]     = ppreg[j];
      msg_to_commit_pub_val[j] = val[j];
    end

    commit_pub.pub( msg_to_commit_pub, msg_to_commit_pub_val );
  endtask

  //----------------------------------------------------------------------
  // Linetracing
  //----------------------------------------------------------------------

  string trace;

  // verilator lint_off BLKSEQ
  always @( posedge clk ) begin
    #2;
    trace = "";

    // trace = {trace, dut.trace( t.trace_level )};
    // trace = {trace, " | "};
    // trace = {trace, alloc_caller.trace( t.trace_level )};
    // trace = {trace, " | "};
    // trace = {trace, lookup_caller[0].trace( t.trace_level )};
    // trace = {trace, " | "};
    // trace = {trace, lookup_caller[1].trace( t.trace_level )};
    // trace = {trace, " | "};
    // trace = {trace, complete_pub.trace( t.trace_level )};
    // trace = {trace, " | "};
    // trace = {trace, commit_pub.trace( t.trace_level )};

    t.trace( trace );
  end
  // verilator lint_on BLKSEQ

  //----------------------------------------------------------------------
  // test_case_1_single_lane_allocate_lookup
  //----------------------------------------------------------------------

  task test_case_1_single_lane_allocate_lookup();
    automatic int urandom_res;

    logic                  [4:0] alloc_areg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_preg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_ppreg [p_num_fe_lanes];
    logic                        alloc_valid [p_num_fe_lanes];

    logic                  [4:0] lookup_areg  [p_num_fe_lanes][2];
    logic [p_phys_addr_bits-1:0] lookup_preg  [p_num_fe_lanes][2];
    logic                        lookup_valid [p_num_fe_lanes][2];

    t.test_case_begin( "test_case_1_single_lane_allocate_lookup" );
    if( !t.run_test ) return;

    for( int i = 0; i < p_num_fe_lanes; i++ ) begin

      // Only allocate on lane 0 for this test
      alloc_areg[i]  = i == 0 ? 5'(3) : 5'(0);
      alloc_preg[i]  = i == 0 ? p_phys_addr_bits'(32) : p_phys_addr_bits'(0);
      alloc_ppreg[i] = i == 0 ? p_phys_addr_bits'(3) : p_phys_addr_bits'(0);
      alloc_valid[i] = i == 0;

      // Only lookup on lane 0 for this test
      for( int j = 0; j < 2; j++ ) begin
        lookup_areg[i][j]  = i == 0 ? 5'(2) : 5'(0);
        lookup_preg[i][j]  = i == 0 ? p_phys_addr_bits'(2) : p_phys_addr_bits'(0);
        lookup_valid[i][j] = i == 0;
      end
    end

    fork
      alloc(
        alloc_areg, 
        alloc_preg, 
        alloc_ppreg, 
        alloc_valid
      );

      lookup_new_inst_srcs(
        lookup_areg, 
        lookup_preg, 
        lookup_valid
      );
    join

    t.test_case_end();
  endtask

  //----------------------------------------------------------------------
  // test_case_2_all_lane_allocate_lookup_no_overlap
  //----------------------------------------------------------------------

  task test_case_2_all_lane_allocate_lookup_no_overlap();
    automatic int urandom_res;

    logic                  [4:0] alloc_areg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_preg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_ppreg [p_num_fe_lanes];
    logic                        alloc_valid [p_num_fe_lanes];

    logic                  [4:0] lookup_areg  [p_num_fe_lanes][2];
    logic [p_phys_addr_bits-1:0] lookup_preg  [p_num_fe_lanes][2];
    logic                        lookup_valid [p_num_fe_lanes][2];

    t.test_case_begin( "test_case_2_all_lane_allocate_lookup_no_overlap" );
    if( !t.run_test ) return;

    // Needs p_num_fe_lanes simultaneously-free physical registers (pregs
    // 32..32+p_num_fe_lanes-1) -- skip for suites too narrow to have that
    // much headroom (e.g. p_num_phys_regs=33 leaves only preg 32 free).
    if( p_num_phys_regs >= 32 + p_num_fe_lanes ) begin
      for( int i = 0; i < p_num_fe_lanes; i++ ) begin

        alloc_areg[i]  = 5'(i + 1);
        alloc_preg[i]  = p_phys_addr_bits'(i + 32);
        alloc_ppreg[i] = p_phys_addr_bits'(i + 1);
        alloc_valid[i] = 1'b1;

        for( int j = 0; j < 2; j++ ) begin
          lookup_areg[i][j]  = 5'(p_num_fe_lanes + (i * 2) + j + 1);
          lookup_preg[i][j]  = p_phys_addr_bits'(p_num_fe_lanes + (i * 2) + j + 1);
          lookup_valid[i][j] = 1'b1;
        end
      end

      fork
        alloc(
          alloc_areg,
          alloc_preg,
          alloc_ppreg,
          alloc_valid
        );

        lookup_new_inst_srcs(
          lookup_areg,
          lookup_preg,
          lookup_valid
        );
      join
    end

    t.test_case_end();
  endtask

  //----------------------------------------------------------------------
  // test_case_3_all_lane_allocate_lookup_with_overlap
  //----------------------------------------------------------------------

  task test_case_3_all_lane_allocate_lookup_with_overlap();
    automatic int urandom_res;

    logic                  [4:0] alloc_areg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_preg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_ppreg [p_num_fe_lanes];
    logic                        alloc_valid [p_num_fe_lanes];

    logic                  [4:0] lookup_areg  [p_num_fe_lanes][2];
    logic [p_phys_addr_bits-1:0] lookup_preg  [p_num_fe_lanes][2];
    logic                        lookup_valid [p_num_fe_lanes][2];

    t.test_case_begin( "test_case_3_all_lane_allocate_lookup_with_overlap" );
    if( !t.run_test ) return;

    // Only do test if more than one fe lane, and there's enough headroom
    // for 2 simultaneously-free physical registers (pregs 32, 33).
    if( p_num_fe_lanes > 1 && p_num_phys_regs >= 34 ) begin
      for( int i = 0; i < p_num_fe_lanes; i++ ) begin

        // First lane allocates a register that the second lane looks up to
        // create an overlap scenario
        // add x1 x2 x3
        // add x5 x1 x1
        if( i < 2 ) begin
          alloc_areg[i]  = i == 0 ? 5'(1) : 5'(5);
          alloc_preg[i]  = p_phys_addr_bits'(i + 32);
          alloc_ppreg[i] = i == 0 ? p_phys_addr_bits'(1) : p_phys_addr_bits'(5);
          alloc_valid[i] = 1'b1;

          lookup_areg[i][0]  = i == 0 ? 5'(2) : 5'(1);
          lookup_preg[i][0]  = i == 0 ? p_phys_addr_bits'(2) : p_phys_addr_bits'(32);
          lookup_valid[i][0] = 1'b1;

          lookup_areg[i][1]  = i == 0 ? 5'(3) : 5'(1);
          lookup_preg[i][1]  = i == 0 ? p_phys_addr_bits'(3) : p_phys_addr_bits'(32);
          lookup_valid[i][1] = 1'b1;
        end else begin
          alloc_areg[i]  = 5'(0);
          alloc_preg[i]  = p_phys_addr_bits'(0);
          alloc_ppreg[i] = p_phys_addr_bits'(0);
          alloc_valid[i] = 1'b0;
          
          for( int j = 0; j < 2; j++ ) begin
            lookup_areg[i][j]  = 5'(0);
            lookup_preg[i][j]  = p_phys_addr_bits'(0);
            lookup_valid[i][j] = 1'b0;
          end
        end
      end

      fork
        alloc(
          alloc_areg, 
          alloc_preg, 
          alloc_ppreg, 
          alloc_valid
        );

        lookup_new_inst_srcs(
          lookup_areg, 
          lookup_preg, 
          lookup_valid
        );
      join
    end

    t.test_case_end();
  endtask

  //----------------------------------------------------------------------
  // test_case_4_some_lane_allocate_lookup_with_overlap
  //----------------------------------------------------------------------

  task test_case_4_some_lane_allocate_lookup_with_overlap();
    automatic int urandom_res;

    logic                  [4:0] alloc_areg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_preg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_ppreg [p_num_fe_lanes];
    logic                        alloc_valid [p_num_fe_lanes];

    logic                  [4:0] lookup_areg  [p_num_fe_lanes][2];
    logic [p_phys_addr_bits-1:0] lookup_preg  [p_num_fe_lanes][2];
    logic                        lookup_valid [p_num_fe_lanes][2];

    t.test_case_begin( "test_case_4_some_lane_allocate_lookup_with_overlap" );
    if( !t.run_test ) return;

    // Only do test if more than two fe lanes, and there's enough headroom
    // for 3 simultaneously-free physical registers (pregs 32, 33, 34).
    if( p_num_fe_lanes > 2 && p_num_phys_regs >= 35 ) begin
      for( int i = 0; i < p_num_fe_lanes; i++ ) begin

        // First lane allocates a register that the second lane looks up to
        // create an overlap scenario
        // add x3 x0 x1
        // add x1 x2 x3
        // add x5 x1 x1
        // invalid
        if( i == 0 ) begin
          alloc_areg[i] = 5'(3);
          alloc_preg[i] = p_phys_addr_bits'(32);
          alloc_ppreg[i] = p_phys_addr_bits'(3);
          alloc_valid[i] = 1'b1;

          lookup_areg[i][0]  = 5'(0);
          lookup_preg[i][0]  = p_phys_addr_bits'(0);
          lookup_valid[i][0] = 1'b1;

          lookup_areg[i][1]  = 5'(1);
          lookup_preg[i][1]  = p_phys_addr_bits'(1);
          lookup_valid[i][1] = 1'b1;
        end else if( i == 1 ) begin
          alloc_areg[i]  = 5'(1);
          alloc_preg[i]  = p_phys_addr_bits'(33);
          alloc_ppreg[i] = p_phys_addr_bits'(1);
          alloc_valid[i] = 1'b1;

          lookup_areg[i][0]  = 5'(2);
          lookup_preg[i][0]  = p_phys_addr_bits'(2);
          lookup_valid[i][0] = 1'b1;

          lookup_areg[i][1]  = 5'(3);
          lookup_preg[i][1]  = p_phys_addr_bits'(32);
          lookup_valid[i][1] = 1'b1;
        end else if( i == 2) begin
          alloc_areg[i]  = 5'(5);
          alloc_preg[i]  = p_phys_addr_bits'(34);
          alloc_ppreg[i] = p_phys_addr_bits'(5);
          alloc_valid[i] = 1'b1;

          lookup_areg[i][0]  = 5'(1);
          lookup_preg[i][0]  = p_phys_addr_bits'(33);
          lookup_valid[i][0] = 1'b1;

          lookup_areg[i][1]  = 5'(1);
          lookup_preg[i][1]  = p_phys_addr_bits'(33);
          lookup_valid[i][1] = 1'b1;
        end else begin
          alloc_areg[i]  = 5'(0);
          alloc_preg[i]  = p_phys_addr_bits'(0);
          alloc_ppreg[i] = p_phys_addr_bits'(0);
          alloc_valid[i] = 1'b0;
          
          for( int j = 0; j < 2; j++ ) begin
            lookup_areg[i][j]  = 5'(0);
            lookup_preg[i][j]  = p_phys_addr_bits'(0);
            lookup_valid[i][j] = 1'b0;
          end
        end
      end

      fork
        alloc(
          alloc_areg, 
          alloc_preg, 
          alloc_ppreg, 
          alloc_valid
        );

        lookup_new_inst_srcs(
          lookup_areg, 
          lookup_preg, 
          lookup_valid
        );
      join
    end

    t.test_case_end();
  endtask

  //----------------------------------------------------------------------
  // test_case_5_pending_lifecycle
  //----------------------------------------------------------------------
  // Exercises the alloc -> pending -> complete -> not-pending lifecycle
  // through lookup_new_inst_pending (areg-indexed).

  task test_case_5_pending_lifecycle();
    logic                  [4:0] alloc_areg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_preg  [p_num_fe_lanes];
    logic [p_phys_addr_bits-1:0] alloc_ppreg [p_num_fe_lanes];
    logic                        alloc_valid [p_num_fe_lanes];

    logic [4:0] lookup_areg  [p_num_fe_lanes][2];
    logic       exp_pending  [p_num_fe_lanes][2];
    logic       lookup_valid [p_num_fe_lanes][2];

    t_complete_msg compl_preg [p_num_be_lanes];
    logic          compl_val  [p_num_be_lanes];

    t.test_case_begin( "test_case_5_pending_lifecycle" );
    if( !t.run_test ) return;

    // ---- Verify initial state: areg 1 and areg 2 are not pending ----
    for( int j = 0; j < p_num_fe_lanes; j++ ) begin
      lookup_areg[j][0]  = 5'd1;
      lookup_areg[j][1]  = 5'd2;
      exp_pending[j][0]  = 1'b0;
      exp_pending[j][1]  = 1'b0;
      lookup_valid[j][0] = 1'b1;
      lookup_valid[j][1] = 1'b1;
    end
    lookup_new_inst_pending( lookup_areg, exp_pending, lookup_valid );

    // ---- Allocate areg 1 on lane 0: areg 1 -> preg 32 (pending) ----
    for( int i = 0; i < p_num_fe_lanes; i++ ) begin
      alloc_areg[i]  = i == 0 ? 5'd1 : 5'd0;
      alloc_preg[i]  = i == 0 ? p_phys_addr_bits'(32) : p_phys_addr_bits'(0);
      alloc_ppreg[i] = i == 0 ? p_phys_addr_bits'(1) : p_phys_addr_bits'(0);
      alloc_valid[i] = i == 0;
    end
    alloc( alloc_areg, alloc_preg, alloc_ppreg, alloc_valid );

    // ---- areg 1 (now pending) and areg 2 (never allocated, not pending) ----
    for( int j = 0; j < p_num_fe_lanes; j++ ) begin
      lookup_areg[j][0]  = 5'd1;
      lookup_areg[j][1]  = 5'd2;
      exp_pending[j][0]  = 1'b1;
      exp_pending[j][1]  = 1'b0;
      lookup_valid[j][0] = 1'b1;
      lookup_valid[j][1] = 1'b1;
    end
    lookup_new_inst_pending( lookup_areg, exp_pending, lookup_valid );

    // ---- Complete preg 32 ----
    for( int j = 0; j < p_num_be_lanes; j++ ) begin
      compl_preg[j] = j == 0 ? p_phys_addr_bits'(32) : 'x;
      compl_val[j]  = j == 0;
    end
    complete( compl_preg, compl_val );

    // ---- areg 1 (no longer pending) and areg 2 (still not pending) ----
    for( int j = 0; j < p_num_fe_lanes; j++ ) begin
      lookup_areg[j][0]  = 5'd1;
      lookup_areg[j][1]  = 5'd2;
      exp_pending[j][0]  = 1'b0;
      exp_pending[j][1]  = 1'b0;
      lookup_valid[j][0] = 1'b1;
      lookup_valid[j][1] = 1'b1;
    end
    lookup_new_inst_pending( lookup_areg, exp_pending, lookup_valid );

    t.test_case_end();
  endtask

  //----------------------------------------------------------------------
  // run_test_suite
  //----------------------------------------------------------------------

  task run_test_suite();
    t.test_suite_begin( suite_name );

    test_case_1_single_lane_allocate_lookup();
    test_case_2_all_lane_allocate_lookup_no_overlap();
    test_case_3_all_lane_allocate_lookup_with_overlap();
    test_case_4_some_lane_allocate_lookup_with_overlap();
    test_case_5_pending_lifecycle();
  endtask
endmodule

//========================================================================
// SSRenameTable_test
//========================================================================

module SSRenameTable_test;

  // Defaults (2 fe lanes, 2 be lanes)
  SSRenameTableTestSuite #(
    .p_suite_num        (1)
  ) suite_1();

  // 1 fe lane, 1 be lane
  SSRenameTableTestSuite #(
    .p_suite_num        (2),
    .p_num_fe_lanes     (1),
    .p_num_be_lanes     (1)
  ) suite_2();

  // 4 fe lanes, 4 be lanes
  SSRenameTableTestSuite #(
    .p_suite_num        (3),
    .p_num_fe_lanes     (4),
    .p_num_be_lanes     (4)
  ) suite_3();

  // 1 fe lane, 4 be lanes
  SSRenameTableTestSuite #(
    .p_suite_num        (4),
    .p_num_fe_lanes     (1),
    .p_num_be_lanes     (4)
  ) suite_4();

  // 4 fe lanes, 1 be lane
  SSRenameTableTestSuite #(
    .p_suite_num        (5),
    .p_num_fe_lanes     (4),
    .p_num_be_lanes     (1)
  ) suite_5();

  // 2 fe lanes, 2 be lanes, 33 phys regs
  SSRenameTableTestSuite #(
    .p_suite_num        (6),
    .p_num_phys_regs    (33)
  ) suite_6();

  int s;

  initial begin
    test_bench_begin( `__FILE__ );
    s = get_test_suite();

    if ((s <= 0) || (s == 1)) suite_1.run_test_suite();
    if ((s <= 0) || (s == 2)) suite_2.run_test_suite();
    if ((s <= 0) || (s == 3)) suite_3.run_test_suite();
    if ((s <= 0) || (s == 4)) suite_4.run_test_suite();
    if ((s <= 0) || (s == 5)) suite_5.run_test_suite();
    if ((s <= 0) || (s == 6)) suite_6.run_test_suite();

    test_bench_end();
  end
endmodule
